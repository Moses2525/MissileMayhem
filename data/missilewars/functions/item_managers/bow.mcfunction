scoreboard players set bowCount m.Variables 1
execute if score @s m.inv_change matches 1 store result score bowCount m.Variables run clear @s #missilewars:bows 0
execute if score bowCount m.Variables matches 0 at @s unless entity @e[type=item,nbt={Item:{id:"minecraft:bow"}},distance=..2] run give @s minecraft:bow{Enchantments:[{lvl: 1s, id:"minecraft:flame"}, {lvl: 4s, id:"minecraft:sharpness"}, {lvl: 1s, id:"minecraft:knockback"}], Unbreakable:1b, display:{Name:'{"text":"Gunblade"}'},CanDestroy:["magenta_wool"]}
execute if score bowCount m.Variables matches 2.. run clear @s #missilewars:bows 1
execute if entity @s[predicate=missilewars:convert_to_crossbow] run item replace entity @s weapon.mainhand with minecraft:crossbow{Enchantments:[{lvl: 1s, id:"minecraft:flame"}, {lvl: 4s, id:"minecraft:sharpness"}, {lvl: 1s, id:"minecraft:knockback"}], Unbreakable:1b, display:{Name:'{"text":"Rocketblade"}'},CanDestroy:["magenta_wool"],ChargedProjectiles:[]}
execute if entity @s[predicate=missilewars:convert_to_bow] run item replace entity @s weapon.mainhand with minecraft:bow{Enchantments:[{lvl: 1s, id:"minecraft:flame"}, {lvl: 4s, id:"minecraft:sharpness"}, {lvl: 1s, id:"minecraft:knockback"}], Unbreakable:1b, display:{Name:'{"text":"Gunblade"}'}}